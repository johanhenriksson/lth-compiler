import java.util.Set;
import java.util.TreeSet;

aspect Errors {
	public class ErrorMessage implements Comparable<ErrorMessage> {
		protected final String message;
		protected final int lineNumber;
        protected final int lineCol;

		public ErrorMessage(String message, int lineNumber, int lineCol) {
			this.message = message;
			this.lineNumber = lineNumber;
            this.lineCol = lineCol;
		}
		public int compareTo(ErrorMessage other) {
			if (lineNumber == other.lineNumber) {
				return message.compareTo(other.message);
			}
			return Integer.compare(lineNumber, other.lineNumber); 
		}
		public String toString() {
			return "Error at line " + lineNumber  + "," + lineCol + ": " + message;
		}
	} 

	protected ErrorMessage ASTNode.error(String message) {
		return new ErrorMessage(message, getLine(getStart()), getColumn(getStart()));
	}

	coll Set<ErrorMessage> Program.errors() [new TreeSet<ErrorMessage>()] with add root Program;

	inh Program ASTNode.program();
	eq Program.getChild().program() = this;
    syn Program Program.program() = this;
}

aspect ErrorContributions {
	IdUse contributes error("symbol '" + getID() + "' is not declared")
		when !isStructId() && decl().isUnknown()
		to Program.errors() for program();

	StructIdUse contributes error("no such field '" + getID() + "' on struct " + getStruct().getID() + " of type " + getStruct().decl().getType().getID())
		when structNameError() 
		to Program.errors() for program();

	IdDecl contributes error("symbol '" + getID() + "' is already declared!")
		when !isUnknown() && isMultiplyDeclared()
		to Program.errors() for program();

    TypeId contributes error("unknown type '" + getID() + "'")
        when invalidType()
        to Program.errors() for program();

    ReturnStmt contributes error("expected return type " + expectedType().toString() + ", got " + getReturnExpr().type().toString())
        when typeError()
        to Program.errors() for program();

    AssignStmt contributes error("cannot assign '" + getName().getID() + "' of type " + getName().type().toString() + ", got " + getAssignExpr().type().toString())
        when !getName().decl().isUnknown() && typeError()
        to Program.errors() for program();

    AssignStmt contributes error("cannot assign '" + getName().getID() + "' because it's a function. dummy!")
        when assignFuncError()
        to Program.errors() for program();

    DeclareStmt contributes error("cannot assign '" + getName().getID() + "' of type " + getName().type().toString() + ", got " + getAssignExpr().type().toString())
        when typeError()
        to Program.errors() for program();

    DeleteStmt contributes error("expected delete operand to be of type struct, got " + getIdUse().type())
        when typeError()
        to Program.errors() for program();

    IfStmt contributes error("expected boolean expression")
        when typeError()
        to Program.errors() for program();

    WhileStmt contributes error("expected boolean expression")
        when typeError()
        to Program.errors() for program();

    BinExpr contributes error("mismatched operands " + getLeft().type() + " and " + getRight().type())
        when typeError()
        to Program.errors() for program();

    FuncExpr contributes error("cannot call '" + getName().getID() + "': not a function")
        when typeError()
        to Program.errors() for program();

    FuncExpr contributes error("cannot call '" + getName().getID() + "': expected " + getName().function().getNumArgument() + " arguments, got " + getNumArgument())
        when getName().isFunction() && paramError()
        to Program.errors() for program();

    FuncExpr contributes error("cannot call '" + getName().getID() + "': parameter type mismatch.\n\tSignature: " + getName().function().signature() + " called with " + signature())
        when getName().isFunction() && !paramError() && paramTypeError()
        to Program.errors() for program();

    IdExpr contributes error("cannot evaluate '" + getName().getID() + "' because it's a function. dummy!")
        when getName().isFunction()
        to Program.errors() for program();

    AddExpr contributes error("mismatched + operands " + getLeft().type() + " and " + getRight().type() + " - must be numeric")
        when !typeError() && nonNumericError()
        to Program.errors() for program();

    SubExpr contributes error("mismatched - operands " + getLeft().type() + " and " + getRight().type() + " - must be numeric")
        when !typeError() && nonNumericError()
        to Program.errors() for program();

    MulExpr contributes error("mismatched * operands " + getLeft().type() + " and " + getRight().type() + " - must be numeric")
        when !typeError() && nonNumericError()
        to Program.errors() for program();

    DivExpr contributes error("mismatched / operands " + getLeft().type() + " and " + getRight().type() + " - must be numeric")
        when !typeError() && nonNumericError()
        to Program.errors() for program();

    ModExpr contributes error("mismatched % operands " + getLeft().type() + " and " + getRight().type() + " - must be numeric")
        when !typeError() && nonNumericError()
        to Program.errors() for program();

    AndExpr contributes error("mismatched && operands " + getLeft().type() + " and " + getRight().type() + " - must be of type bool")
        when !typeError() && nonBoolError()
        to Program.errors() for program();

    OrExpr contributes error("mismatched || operands " + getLeft().type() + " and " + getRight().type() + " - must be of type bool")
        when !typeError() && nonBoolError()
        to Program.errors() for program();

    NotExpr contributes error("mismatched ! operand " + getExpr().type() + " - must be of type bool")
        when nonBoolError()
        to Program.errors() for program();

    Struct contributes error("Struct " + getName().getID() + " overrides builtin type")
        when overridesBuiltinType()
        to Program.errors() for program();

    Struct contributes error("Struct " + getName().getID() + " cannot be declared more than once")
        when isMultiplyDeclared()
        to Program.errors() for program();
}
