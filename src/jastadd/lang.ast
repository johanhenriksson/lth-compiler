/* Goal: Program */
Program  ::= Unit*;

abstract Unit;
Global : Unit ::= DeclareStmt;

/* Functions */
Function : Unit ::= Type:TypeId Name:IdDecl Argument* Block;
Argument ::= Type:TypeId Name:IdDecl;

/* Statements */
abstract Statement;

Block        : Statement ::= Statement*;
AssignStmt   : Statement ::= Name:IdUse AssignExpr:Expr;
DeclareStmt  : Statement ::= Type:TypeId Name:IdDecl [ AssignExpr:Expr ];
WhileStmt    : Statement ::= Condition:Expr Loop:Statement;
IfStmt       : Statement ::= Condition:Expr Then:Statement [ Else:Statement ];
CallStmt     : Statement ::= Function:FuncExpr;
ReturnStmt   : Statement ::= [ ReturnExpr:Expr ];
BreakStmt    : Statement;
ContinueStmt : Statement;

/* Expressions */
abstract Expr;
abstract BinExpr : Expr ::= Left:Expr Right:Expr;

/* Variables & Function calls */
FuncExpr : Expr ::= Name:IdUse Argument:Expr*;

/* Logical operators */
NotExpr : Expr ::= Expr;
AndExpr : BinExpr;
OrExpr  : BinExpr;

/* Comparison operators */
EqualsExpr       : BinExpr;
NotEqualsExpr    : BinExpr;
LessExpr         : BinExpr;
LessEqualExpr    : BinExpr;
GreaterEqualExpr : BinExpr;
GreaterExpr      : BinExpr;

/* Arithmetic operators */
AddExpr : BinExpr;
SubExpr : BinExpr;
MulExpr : BinExpr;
DivExpr : BinExpr;
ModExpr : BinExpr;

/* Literals */
abstract Literal : Expr;

IntLiteral    : Literal ::= <INT:String>;
BoolLiteral   : Literal ::= <BOOL:String>;
FloatLiteral  : Literal ::= <FLOAT:String>;
StringLiteral : Literal ::= <STRING:String>;

/* Identifiers */
IdUse : Expr ::= <ID:String>;
IdDecl ::= <ID:String>;

/* Type Names */
TypeId ::= <ID:String>;
