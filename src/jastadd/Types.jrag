aspect Types {
    syn Type Program.findType(String name) {
        for(int i = 0; i < getNumUnit(); i++) {
            Struct struct = getUnit(i).findStruct(name);
            if (struct != null)
                return new StructType(struct);
        }
        return new UnknownType();
    }

    syn Struct Unit.findStruct(String name) = null;
    eq Struct.findStruct(String name) {
        if (getName().getID().equals(name))
            return this;
        return null;
    }

    syn boolean Type.isUnknownType() = false;
    eq UnknownType.isUnknownType() = true;

    syn Type PointerTypeId.type() = new PointerType(super.type());
    syn Type TypeId.type() {
        switch(getID()) {
            case "int": return new IntType();
            case "bool": return new BoolType();
            case "void": return new VoidType();
            case "float": return new FloatType();
            case "string": return new StringType();
        }

        // look for struct
        return program().findType(getID());
    }

    syn String StructType.name() = getStruct().getName().getID();

    // helper for dereference operator
    syn Type Type.dereference() = new UnknownType();
    eq PointerType.dereference() = getTo();

    syn boolean Type.equals(Object other) {
        if (!(other instanceof Type))
            return false;
        return toString().equals(((Type)other).toString());
    }

    syn String IntType.toString() = "int";
    syn String BoolType.toString() = "bool";
    syn String VoidType.toString() = "void";
    syn String FloatType.toString() = "float";
    syn String StringType.toString() = "string";
    syn String PointerType.toString() = getTo() + "*";
    syn String UnknownType.toString() = "<unknown>";

    syn String StructType.toString() = "struct " + getStruct().getName();
}
